; Создаем файл двоичных данных при помощи генератора случайных чисел
format PE console ;GUI 4.0
entry start

include 'win32a.inc'
section '.data' data readable writeable
;;;;;;;;;;;;;;;;;;;;
FilePath db 'd:\Lit\Uni\Matrix_multiply\get_data\data.bin',0 ;Camac_Thread_data_000.bin
hFile dd 0
BytesRead dd 0
BytesWritten dd 0
Buffer rb 400        ;Размер файла Матрицы в байтах
Buffer.size dd 400    ;equ ($ - Buffer) ; Calculates the size at compile time
NULL equ 0


out_message db 'Some random value : %u',0
bottom_edge    dd 0x0000003C
top_edge    dd 0x7FFFFFFE    ; 4294967294 
numElement    dd 100         ; Число СЛОВ (1/2/4 байта), записанных в файл
counter     dd 0           ; счетчик цикла для случайных чисел
; Данные для генератора случайных чисел
JJ      =       10             ; lag 1
KK      =       17             ; lag 2, size of circular buffer
R1      =       19             ; rotate count
R2      =       27             ; rotate count
randp1  dt      1.5            ; used for conversion to float
        dw      0              ; alignment
p1      dd      0              ; pointer in circular buffer
p2      dd      0              ; pointer in circular buffer
randbuf dd      (2*KK) dup(?)  ; circular buffer



section '.code' code readable executable

start:
        ; Инициализация генератора случайных чисел
        invoke  GetTickCount
        stdcall WRandomInit,eax

        ;stdcall WRandom
        ; В регистрах EDX:EAX случайное число

rnd_cycle: ; Получаем случайное число, пишем его в буфер
            stdcall WIRandom,[bottom_edge],[top_edge] ;0xFFFFFFFE
             ; В регистре EAX случайное число
            
            mov ecx, [counter] ;
            imul ecx,4
            add ecx, Buffer
            mov [ecx], eax
            inc [counter]
            
            mov edx, [numElement]
            cmp [counter], edx
            jne rnd_cycle

            mov [counter], 0
           
Write_Buf: ; Пишем буфер в файл

            invoke CreateFileA,FilePath,GENERIC_WRITE,NULL,NULL,CREATE_ALWAYS,FILE_ATTRIBUTE_NORMAL,NULL 
            mov [hFile],eax
            ; write Buffer.size byte from Buffer to the file; actual bytes written put into BytesWritten
            invoke WriteFile,[hFile],Buffer,[Buffer.size],[BytesWritten],NULL 
            invoke CloseHandle,[hFile] ; done



            mov eax, Buffer
            add eax, 8

            push dword [eax]
            push out_message
            call [printf]        ; Выводим в консоль значение, сохраненное в  EAX.



call [ExitProcess] ; Go back to the calling procedure (system, whatever)


;---------------------------------------------
; Получить случайное число
; stdcall WRandom
; на выходе EDX:EAX - случайное число
;---------------------------------------------
proc    WRandom
        stdcall WBRandom
        or      edx, 80000000h
        mov     dword [randp1],eax
        mov     dword [randp1+4],edx
        fld1
        fld     [randp1]
        ret
endp
 
;---------------------------------------------
; Получить случайное число в нужном интервале
; stdcall WIRandom,min,max
; на выходе EAX - случайное число
; EAX=80000000h - ошибка
;---------------------------------------------
proc    WIRandom rmin:dword,rmax:dword
        push    ebx
 
        stdcall WBRandom
        ; Проверить корректность задания границ, MIN<MAX
        mov     ebx,[rmax]
        sub     ebx,[rmin]
        js      .WIRandom_1
        inc     ebx
        mov     ecx,edx
        mul     ebx
        mov     eax,ecx
        mov     ecx,edx
        mul     ebx
        add     eax,ecx
        adc     edx,[rmin]
        mov     eax,edx
 
        pop     ebx
        ret
 
.WIRandom_1:
        mov     eax,80000000h
        pop     ebx
        ret
endp
 
;---------------------------------------------
; Инициализация генератора случайных чисел
; stdcall WRandomInit,seed
;---------------------------------------------
proc    WRandomInit seed:dword
        mov     eax,[seed]
        xor     ecx,ecx
.WRandomInit_1:
        imul    eax,2891336453
        inc     eax
        mov     [randbuf+ecx*4],eax
        inc     ecx
        cmp     ecx,KK*2
        JB      .WRandomInit_1
 
        fld1
        fstp    [randp1]
        mov     [p1],0
        mov     [p2],JJ*8
        stdcall WBRandom
 
        push    edi
 
        mov     edi,30
.WRandomInit_2:
        stdcall WBRandom
        dec     edi
        jnz     .WRandomInit_2
 
        pop     edi
        ret
endp
 
;---------------------------------------------
; Вспомогательная процедура генератора
; В пользовательском не вызывается
;---------------------------------------------
proc    WBRandom
        push    ebx
        mov     ebx, [p1]
        mov     ecx, [p2]
        mov     edx, [randbuf+ebx]
        mov     eax, [randbuf+ebx+4]
 
        rol     edx, R1
        rol     eax, R2
        add     edx, [randbuf+ecx]
        add     eax, [randbuf+ecx+4]
        mov     [randbuf+ebx], eax
        mov     [randbuf+ebx+4], edx
        sub     ebx, 8
        jnc     .WBRandom_1
        mov     ebx, (KK-1)*8
.WBRandom_1:
        sub     ecx, 8
        jnc     .WBRandom_2
        mov     ecx, (KK-1)*8
.WBRandom_2:
        mov     [p1], ebx
        mov     [p2], ecx
        pop     ebx
        ret
endp

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;start:






section '.idata' import data readable
 
library kernel32, 'kernel32.dll', \
        msvcrt,'msvcrt.dll'

import kernel32, \
       ExitProcess,'ExitProcess', \
       CreateFileA,'CreateFileA', \
       ReadFile, 'ReadFile', \
       WriteFile,'WriteFile', \
       CloseHandle, 'CloseHandle', \
       GetTickCount, 'GetTickCount'
 
import  msvcrt, \
        printf, 'printf'

